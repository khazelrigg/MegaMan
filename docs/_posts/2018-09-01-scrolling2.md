---
title: "Creating a Scrolling Background - pt2"
date: 2018-09-01
category: code
header:
    teaser: /assets/img/scrolling/teaser2.png
gallery:
    - url: /assets/img/scrolling/leftLimit.jpg
      image_path: assets/img/scrolling/leftLimit.jpg
      title: "Player standing at left edge of map"
      alt: "Standing at far left"
    - url: /assets/img/scrolling/center.jpg
      image_path: assets/img/scrolling/center.jpg
      title: "Player standing still, centered on screen"
      alt: "Standing in center"
gallery2:
    - url: /assets/img/scrolling/running.jpg
      image_path: assets/img/scrolling/running.jpg
      title: "Player running to right, centered on screen"
      alt: "Running across"
    - url: /assets/img/scrolling/rightLimit.jpg
      image_path: assets/img/scrolling/rightLimit.jpg
      title: "Player standing at right edge of map"
      alt: "Standing at far right"
---

### Movement Based on Player Position

In [part one]({{ site.baseurl }}{% post_url 2018-08-31-scrolling %}) of creating a scrolling background, we created our abstract class `Level` and built our `BombStage` class which allowed us to move our background using keyboard input.
Building off of what we have we can add our player and use our player to move the background.

Let's look at how the original game used player position.

{% include gallery %}
{% include gallery id="gallery2" caption="Screenshots of player position from Mega Man." %}

Looking at screen shots from the original game we can see that our player does not move away from the center of our x-axis unless we are at the very far left or right edges of the map.

To begin we can remove movement from `drawLevel` since it is no longer needed and trim it down to the following:

{% highlight java linenos %}
class BombStage {
    ...    
    void drawLevel() {
        translate(-pos.x, -pos.y);
        imageMode(CORNER);
        image(bgImg, 0, 0);
    }
    ...
}
{% endhighlight %}

For us to draw our background using player position we first need to create our player class that keeps track of player position, velocity, direction and eventually will contain other information like health and lives.

{% highlight java linenos %}
class Player {
  PImage img;
  PVector pos;
  PVector velocity = new PVector(0, 0);
  float jumpSpeed = 6;
  float walkSpeed = 2;
  float direction = 1;
  float left, right, up, down;

  Player() {
    pos = new PVector(100, 159);
    img = loadImage("mmSprites/right1.png");
  }

  void update() {
    move();
  }
  
  void move() {
    int groundY = 185 - img.height / 2;
		
    if (pos.y < groundY ) {
      velocity.y += 0.5; //Gravity if we are not at bottom of scren
    } else {
      velocity.y = 0;
      pos.y = groundY; //If we are on/below the ground move us to the ground
    }

    if (pos.y >= groundY && up != 0) {
      velocity.y = -jumpSpeed;
    }
    velocity.x = walkSpeed * (left + right);

    PVector futurePos = new PVector(pos.x, pos.y);
    futurePos.add(velocity);

    if(futurePos.x > 0 && futurePos.x < width) {
      pos.x = futurePos.x;
    }

    if (futurePos.y > 0 && futurePos.y < height) {
      pos.y = futurePos.y;
    }

    pushMatrix();
    translate(stage.pos.x + pos.x, stage.pos.y + pos.y);
    scale(direction, 1); // Scaling with direction means we can flip our image if moving left/right
    imageMode(CENTER);
    image(img, 0, 0);
    popMatrix();
  }
}
{% endhighlight %}

Now lets add a few functions to our main class that change our player's velocity

{% highlight java linenos %}
BombStage stage;
Player megaman;

void setup() {
  size(256, 224);
  frameRate(60);
  stage = new BombStage();
  megaman = new Player();
}

void draw() {
  stage.drawLevel();
  megaman.update();
}


void keyPressed() {
 if  (key == 'd')
  {
    megaman.right = 1;
    megaman.direction = 1;
  }
  if (key == 'a')
  {
    megaman.left = -1;
    megaman.direction = -1;
  }
  if (key == 'w')
  {
    megaman.up = -1;
  }
  if (key == 's')
  {
    megaman.down = 1;
  }
}

void keyReleased()
{
  if (key == 'd')
  {
    megaman.right = 0;
  }
  if (key == 'a')
  {
    megaman.left = 0;
  }
  if (key == 'w')
  {
    megaman.up = 0;
  }
  if (key == 's')
  {
    megaman.down = 0;
  }
}
{% endhighlight %}

<p>Note: Our players movement is heavily inspired by <a href="https://www.openprocessing.org/sketch/92234">this sketch</a></p>{: .notice notice--primary}

The resulting sketch now allows us to move the player. With a couple changes to our `drawLevel` function we will also be able to move our background based on where our player is positioned.
<iframe src="https://www.openprocessing.org/sketch/584557/embed/" width="400" height="284"></iframe>{: .align-center}

### Making the background move with the player

Our Level is responsible for moving itself so our changes only need to be focused there. We know that when our player moves, we want to translate our image in the opposite direction so it appears that we are moving across the world.

To add our right and left movement we can modify the `drawLevel` function as follows:
{% highlight java linenos %}
  void drawLevel() {
    boolean rightOfCenter = megaman.pos.x > width / 2;
    boolean leftOfCenter = megaman.pos.x < width / 2;
    
    // We only want to move the background if the player is holding down a key for movement
    if (keyPressed) {
      if ((rightOfCenter && megaman.direction > 0) && canMoveRight()) {
          pos.x += megaman.walkSpeed;
          megaman.pos.x = width / 2; //Move player back to center
      } else if ((leftOfCenter && megaman.direction < 1) && canMoveLeft()) {
          pos.x -= megaman.walkSpeed;
          megaman.pos.x = width / 2;
      }
    }
    
    translate(-pos.x, -pos.y);
    imageMode(CORNER);
    image(bgImg, 0, 0);
  }
{% endhighlight %}

Now our background can move left and right depending on the players movement.
<iframe src="https://www.openprocessing.org/sketch/584570/embed/" width="400" height="284"></iframe>{: .align-center}

Next we will be adding some animation to our character.
